.PROGRAM main()
        CALL init() ; Tar in startvärden


; Skapar pickframe
        SET pickframe = FRAME(frame1[0],frame1[1],frame1[2],frame1[0])


; Skapar placeframe
        SET placeframe = FRAME(frame2[0],frame2[1],frame2[2],frame2[0])
; skapa home
        SET home = SHIFT(frame1[0] BY -50,-25,100) ; Home ska placeras i mitten av pickframen


; inta startläge
        SPEED 25 ALWAYS
        MOVE home
        BREAK

; Loop
        WHILE arbete DO


            SET pick = pickframe:TRANS(x*x_avs,y*y_avs) ; Förskjutning från pickframe (origin)
            SET place = placeframe:TRANS(x*x_avs,y*y_avs) ; Förskjutning från placeframe (origin)


; Pick

            RIGHTY ; För att nå på sin vänstra sida
            BREAK

            APPRO pick, 30
            SPEED 25 ALWAYS
            CALL gripper(open)
            MOVES pick
            BREAK
            WAIT.EVENT 0.2
            CALL gripper(close)
            DEPART 30

;Labyrint Pick-Place
            SPEED 80 ALWAYS
            FOR i = 0 TO 13
                MOVES loc[i]
            END


; Place

            LEFTY ; För att nå sin höger sida
            BREAK

            APPRO place, 30
            SPEED 25 ALWAYS
            MOVES place
            BREAK
            CALL gripper(open)
            WAIT.EVENT 0.2
            DEPART 30

; Labyrint Place-Pick
            SPEED 80 ALWAYS
            FOR i = 13 TO 0 STEP -1
                MOVES loc[i]
            END
; Uppräknare för förskjutning i X och Y axel för pick/place-frame
            x = x+1
            IF x > x_grid THEN
                x = 0
                y = y+1
            END
            IF y > y_grid THEN
                y = 0
                arbete = FALSE ;klar
                TYPE "Arbetet klart"
                SPEED 25 ALWAYS
                MOVE home
            END
        END

    ; Slut loop

.END

